$title HN123D
*ELABORATED ON THE BASIS OF DEVARAJAN S. 1997, VERSION FOR EXCEL BY R.MORALES
*November 2005

SETS  T /2004*2018/;
PARAMETERS
*RELATED TO PRODUCTION FUNCTION................................
GRL     TASA DE CRECIMIENTO DE LA POBLACION  /0.026/
DELTA  TASA DE DEPRECIACION   /0.05/
AE      PARAMETRO DE EFICIENCIA O TECNOLOGIA /1.123/
L0     VOLUMEN DE LA POBLACION A T=0
L(T)   EFECTIVOS POBLACIONALES
K0     STOCK DE CAPITAL A T=0
R      TASA DE DESCUENTO         /0.03/
ALFA   ELASTICIDAD PRDUCTO CAPITAL  /0.546/
F1     CONSTANTE DE LA FUNCION ICOR /4.2314/
F2       PARAMETRO DE FBC /37.0995/
F3       PARAMETRO DE FBC(-1) /-35.238/
F4       PARAMETRO DE PIB(-1) EN LA FUNCION FBC /-0.3197/
F5       PARAMETRO DE LA DUMMY EN LA FUNCION FBC /-5.2730/
DUMMY(T) VARIABLE DUMMY EXOGENA
 /
  "2004" 0.0
 "2005" 0.0
  "2006" 0.0
 "2007" 0.0
 "2008" 0.0
 "2009" 0.0
 "2010" 0.0
 "2011" 0.0
  "2012" 0.0
 "2013" 0.0
 "2014" 0.0
   "2015" 0.0
"2016" 0.0
"2017" 0.0
"2018" 0.0/;

PARAMETERS
*EXOGENOUS VARIABLES OF THE 1-2-3 MODEL...........................................
TM       TARIFF RATE ARANCELES /0.0276/
TE       EXPORT SUBSIDY RATE  /0.0/
TS       SALE VALUE ADDED TAX RATE /0.109/
TY       DIRECT TAX RATE ON PRIVATE INCOME /0.0351/
SY       SAVING RATE /0.25/

G(T)     REAL GOVERNMENT DEMAND CONSUMPTION
TR       GOVERNMENT TRANSFERS /0.046/
FT       FOREIGN TRANSFERS TO GOVERNMENT /0.033/
RE       FOREING REMITTANCES TO PRIVATE SECTOR /0.064/


PARAMETERS
*FOREING DEBT PARAMETERS......................................................
RATE     INTERNATIONAL INTEREST RATE/0.0276/
DEBT0    INITIAL STOCK OF DEBTS /0.821/
*BALANCE OF PAYMENTS
B(T)     BALANCE OF TRADE-CONSTANTE;
B(T)=0.04257;
PARAMETERS
* INICIAL VALUES FOR SOME ENDOGENOUS VARIABLES
*REAL VALUE
X21      GDP ANO 2003 /120322/
X0       GDP INICITAL NORMALIZADO /1.0/
GRW0     TASA INICIAL DE CRECIMIENTO DEL PIB EN % /3.5/
G0       YEAR BASE REAL GOVERNMENT DEMAND CONSUMPTION  /0.159/
E0       EXPORT GOOD /0.39355/
M0       IMPORT GOOD /0.55957/
DS0      SUPPLY OF DOMESTIC GOOD
DD0      DEMAND FOR DOMESTIC GOOD
QS0      SUPPLY OF COMPOSITE GOOD
QD0      DEMAND OF COMPOSITE GOOD
Z0       INVESTMENT/0.27436/
Y0       INGRESO HOGARES/1.11/
*PRICES
PM0      IMPORT PRICE /1.0/
PE0      EXPORT PRICE/1.0/
PD0      PRICE OF DOMESTIC GOOD /1.0/
PQ0      PRICE OF COMPOSITE GOOD /1.0/
PT0      SALES PRICE /1.0/
PX0      GDP DEFLACTOR /1.0/
PWM0     BASE YEAR WORLD PRICE OF IMPORT GOOD /1.0/
PWE0     BASE YEAR WORLD PRICE OF EXPORT GOOD /1.0/;

L0= 3031.135/X21;
K0= 80229.689/X21;

DS0=0.728;
DD0=0.728;
*QS0=1.28757;
*QD0=1.28757;
QS0=1.2;
QD0=1.2;
PARAMETERS
*CALIBRACION DE LAS FUNCIONES CET Y CES
SQ       ELASTICITY FOR CESQ /0.85/
ST       ELASTICITY FOR CET/0.60/
RT       RHO FOR CET
BT       SHARE FOR CET
AT       SCALE FOR CET
RQ       RHO PARA CESQ
BQ       SHARE FOR CESQ
AQ       SCALE FOR CESQ;

*PARA LA FUNCION CET/X
RT=1/ST+1;
BT=1/(1+(PD0/PE0)*(E0/DS0)**(RT-1));
AT=X0/((BT*E0**RT+(1-BT)*DS0**RT)**(1/RT));
*PATA LA FUNCION CES/Q
RQ=1/SQ-1;
BQ=((PM0/PD0)*(M0/DD0)**(1+RQ))/(1+(PM0/PD0)*(M0/DD0)**(1+RQ));
AQ=QS0/((BQ*M0**(-RQ)+(1-BQ)*DD0**(-RQ))**(-1/RQ));


PARAMETERS
*ECUACIONES PRELIMINARES A LA RESOLUCION DEL BLOQUE
ER(T)    19 NUMERAIRE EXCHAGE RATE (DOLARES POR BOLIVIANO)
PM(T)    14 IMPORT PRICE EQUATION
PE(T)    15 EXPORT PRICE EQUATION
PWM(T)   WORLD PRICE OF IMPORT GOOD
PWE(T)   WORLD PRICE OF EXPORT GOOD;

ER(T)=1;
PWM(T)=PWM0*(1.035)**(ORD(T));
PWE(T)=PWE0*(1.0)**(ORD(T));
PM(T)=ER(T)*PWM(T)*(1+TM);
PE(T)=ER(T)*PWE(T)/(1+TE);
L(T)=L0*(1+GRL)**(ORD(T));
G(T)=G0*(1+GRL)**(ORD(T));
*G(T)=G0;
VARIABLES
*REAL FLOWS
FBC(T)      INVERSION SOBRE PIB EN PORCENTAJE
GRW(T)      TASA DE CRECIMIENTO DEL PIB EN PORCENTAJE
X(T)        1 REAL OUTPUT
C(T)        4 REAL AGGREGATE CONSUMPTION
*CC(T)       5CONSUMO PER CAPITA
E(T)        6 REAL EXPORTS
M(T)        7 REAL IMPORTS
DS(T)       8 REAL SUPPLY OF DOMESTIC GOOD
DD(T)       9 REAL DEMAND FOR DOMESTIC GOOD
QS(T)       10 REAL SUPPLY OF COMPOSITE GOOD
QD(T)       11 REAL DEMAND OFOR COMPOSITE GOOD
Z(T)        12 REAL AGGREGATE INVESTMENT
*PRICES
PD(T)       15PRODUCER PRICE OF DOMESTIC GOOD
PT(T)       16SALE PRICE OF COMPOSITIE GOOD
PX(T)       17PRICE OF AGGREGATE OUTPUT
PQ(T)       18PRICE OF COMPOSITE GOOD
*NOMINAL
Y(T)        23TOTAL NOMINAL INCOME
DEBT(T)     DEBT ACCUMULATION IN US DOLLAR
RENFE(T)     INTEREST ON LOANS IN US DOLLAR;
* INITIAL VALUES
E.L(T)=E0;
M.L(T)=M0;
DS.L(T)=DS0;
DD.L(T)=DD0;
QS.L(T)=QS0;
QD.L(T)=QD0;
Z.L(T)=Z0;
X.L(T)=X0;
QS.L(T)=QS0;
Y.L(T)=Y0;
FBC.L(T)=Z0;
GRW.L(T)=GRW0;
*INITIAL PRICES VALUES
PT.L(T)=PT0;
PD.L(T)=PD0;
PQ.L(T)=PQ0;
PX.L(T)=PX0;

EQUATIONS
*THE DYNAMIC OF THE MODEL...................................................
QFBC(T)       2B DEFINICION DE FBC EN PORCENTAJE
QGRW(T)       2C TASA DE CRECIMIENTO DEL PIB
QX(T)         3FUNCION DE PRODUCCION
*THE 1-2-3 MODEL............................................................
CETEQ(T)      5CET TRANSFORMATION
ARMG(T)       6SYPPLY OF GOODS
DEM(T)        7DOMESTIC DEMAND
EDRAT(T)      8E D RATIO
MDRAT(T)      9M D RATIO
INC(T)        11TOTAL HOUSEHOLD INCOME
CONS(T)       13PRIVATE CONSUMPTION FUNCTION
PTEQ(T)    16SALES PRICE EQUATION
PXEQ(T)    17OUTPUT PRICE EQUATION
PQEQ(T)    18SUPPLY PARICE EQUATION
DEQ(T)     20EQUILIBRIUM IN THE DOMESTIC GOOD MARKET
QEQ(T)     21EQUILIBRIUM IN THE COMPOSITE GOOD MARKET
CABAL(T)   22EQUILIBRIUM IN THE COURRANT ACCOUNT BALANCE
QDEBT(T)   DEBT ACCUMULATION
QRENFE(T)   INTERESTS ON LOANS;

*THE EQUATIONS................................................................
*the dinamic of the model
QFBC(T)..   FBC(T)*X(T)=E=Z(T);
QGRW(T)..   GRW(T)=E=GRW0$(ORD(T)=1)+(F1+F2*FBC(T)+F3*FBC(T-1)+F4*GRW(T-1)+F5*DUMMY(T))$(ORD(T) GT 1);
QX(T)..     X(T)=E=X0*(1+GRW0/100)$(ORD(T)=1)+((1+GRW(T)/100))*X(T-1)$(ORD(T) GT 1);
*the 1-2-3 model
CETEQ(T)..  X(T)=E=AT*(BT*E(T)**(RT)+(1-BT)*DS(T)**(RT))**(1/RT);
ARMG(T)..   QS(T)=E=AQ*(BQ*M(T)**(-RQ)+(1-BQ)*DD(T)**(-RQ))**(-1/RQ);
DEM(T)..    Z(T)=E=QD(T)-C(T)-G(T);
EDRAT(T)..  E(T)=E=DS(T)*((PE(T)/PD(T))/(BT/(1-BT)))**(1/(RT-1));
MDRAT(T)..  M(T)=E=DD(T)*((PD(T)/PM(T))*(BQ/(1-BQ)))**(1/(RQ+1));
*NOMINAL
INC(T)..    Y(T)=E=PX(T)*X(T)+TR*PQ(T)+RE*ER(T);
*REAL
CONS(T)..   C(T)=E=Y(T)*(1-TY-SY)/PT(T);
*PRICES
PTEQ(T)..  PT(T)=E=PQ(T)*(1+TS);
PXEQ(T)..  PX(T)*X(T)=E=(PE(T)*E(T)+PD(T)*DS(T));
PQEQ(T)..  PQ(T)*QS(T)=E=(PM(T)*M(T)+PD(T)*DD(T));
*EQUILIBRIUM CONDITIONS
DEQ(T)..   DD(T)=E=DS(T);
QEQ(T)..   QD(T)=E=QS(T);
CABAL(T).. (PWM(T)*M(T)+RENFE(T))-PWE(T)*E(T)-FT-RE=E=B(T);
*DEBT RELATIONS
QDEBT(T)..   DEBT(T)=E=DEBT0$(ORD(T)=1)+B(T)+DEBT(T-1)$(ORD(T) GT 1);
QRENFE(T)..  RENFE(T)=E=RATE*DEBT(T);

Model model1 square base model / all / ;
*SOLVE MODEL1 USING DNLP MINIMMIZING DIF ;
Solve model1 using MCP;

PARAMETERS
*ECUACIONES POSTERIORES A LA RESOLUCION DEL BLOQUE
TAX(T)   10 GOVERNMENT NOMINAL REVENU
SG(T)    23 GOVERNMENT NOMINAL INCOME OR SAVING
S(T)     12 NOMINAL TOTAL SAVING
YREAL(T) INGRESO NOMINAL Y DEFLACTADO POR PT
CC(T)    CONSUMO REAL PER CAPITA
SPRIV(T) AHORRO PRIVADO
INTG(T)  INTERES DE LA DEUDA EXTERNA EN DOLARES
TRANS(T)  TRANSFERENCIAS DEL GOBIERNO AL SECTOR PRIVADO
FOREI(T)  TRANSFERENCIAS DEL EXTRANJERO AL GOBIERNO
TRAD(T)   INVESTMENT LESS TOTAL SAVING
TRADB(T)  COURRANT ACCOUNT;

*COMPLEMENTARY EQUATIONS
*NOMINAL VARIABLES...............
TAX(T)=TM*PWM(T)*ER(T)*M.L(T)-TE*PE(T)*E.L(T)+TS*PQ.L(T)*QD.L(T)+TY*Y.L(T)+FT*ER(T);
SG(T)=TAX(T)-G(T)*PT.L(T)-TR*PQ.L(T)-RENFE.L(T)*ER(T);
S(T)=SY*Y.L(T)+ER(T)*B(T)+SG(T);
*REAL VARIABLES..............
CC(T)=C.L(T)/L(T);
YREAL(T)=Y.L(T)/PT.L(T);
*VERIFICACION S=I Y CUMPLIMIENTO BALANZA DE PAGOS..............
TRAD(T)=PT.L(T)*Z.L(T)-S(T);
TRADB(T)=PWM(T)*M.L(T)-PWE(T)*E.L(T)+RENFE.L(T)-FT-RE;

*PARA EL ANALISIS POSTERIOR
TRANS(T)= TR*PQ.L(T);
FOREI(T)= FT*ER(T);
SPRIV(T)=S(T)-SG(T)-ER(T)*B(T);

DISPLAY TAX,SG,S;
DISPLAY TRAD,TRADB;
DISPLAY X.L,E.L,M.L,GRW.L,Z.L,PX.L;
DISPLAY ST,SQ,RT,BT,AT;
DISPLAY RQ,BQ,AQ;
DISPLAY X0;
PARAMETERS
PXPIB(T)         PIB PARAMETRIZADO
GRPIB(T)         TASA DE CRECIMIENTO DEL PIB
PCPRI(T)         CONSUMO PARAMETRIZADO
PINVE(T)         INVERSION PARAMETRIZADA
PEXPO(T)         EXPORTACION PARAMETRIZADA
PIMPO(T)         IMPORTACION PARAMETRIZADA
PQSCO(T)         BIEN COMPUESTO PARAMETRIZADO
PDSDE(T)         DEMANDA DOMESTICA PARAMETRIZADA
PRENF(T)         RENTA NETA DE FACTORES EXTERNOS
PDEBT(T)         STOCK DEUDA
PRFAM(T)         INGRESO DE LAS FAMILIAS
PDOME(T)         PRECIO BIENES DOMESTICOS
PCOMP(T)         PRECIO DE VENTA DEL BIEN COMPUESTO
PPIB(T)          DEFLACTOR DEL PIB
PQSP(T)          PRECIO DEL BIEN COMPUESTO;

PXPIB(T)=X.L(T);
GRPIB(T)=GRW.L(T);
PCPRI(T)=C.L(T);
PINVE(T)=Z.L(T);
PEXPO(T)=E.L(T);
PIMPO(T)=M.L(T);
PQSCO(T)=QS.L(T);
PDSDE(T)=DS.L(T);
PRENF(T)=RENFE.L(T);
PDEBT(T)=DEBT.L(T);
PRFAM(T)=Y.L(T);
PDOME(T)=PD.L(T);
PCOMP(T)=PT.L(T);
PPIB(T)=PX.L(T);
PQSP(T)=PQ.L(T);

*$LIBINCLUDE C:\ARCHIVOS DE PROGRAMA\GAMS22\INCLIB\XLDUMP.GMS;
*$ontext
*CUENTAS NACIONALES A PRECIOS CONSTANTES
$libinclude xldump PXPIB HON123D.xls X
$libinclude xldump GRPIB HON123D.xls GRPIB
$libinclude xldump G HON123D.xls G
$libinclude xldump PCPRI HON123D.xls C
$libinclude xldump PINVE HON123D.xls Z
$libinclude xldump PEXPO HON123D.xls E
$libinclude xldump PIMPO HON123D.xls M
$libinclude xldump PQSCO HON123D.xls Q
$libinclude xldump PDSDE HON123D.xls D

*BALANZA DE PAGOS EN DOLARES

$libinclude xldump PRENF HON123D.xls RENFE
$libinclude xldump B HON123D.xls B
$libinclude xldump PDEBT HON123D.xls DEBT
*CUENTAS SECTORIALES
$libinclude xldump PRFAM   HON123D.xls RFAM
$libinclude xldump SPRIV HON123D.xls SPRIV
$libinclude xldump TAX   HON123D.xls RGOV
$libinclude xldump SG    HON123D.xls SG
$libinclude xldump S     HON123D.xls S
$libinclude xldump TRANS HON123D.xls TRAN
$libinclude xldump FOREI HON123D.xls FOREI
*PRECIOS
$libinclude xldump PDOME  HON123D.xls PD
$libinclude xldump PCOMP HON123D.xls PT
$libinclude xldump PPIB HON123D.xls PX
$libinclude xldump PQSP  HON123D.xls PQ
$libinclude xldump PM    HON123D.xls PM
$libinclude xldump PE    HON123D.xls PE
$libinclude xldump ER    HON123D.xls ER
$libinclude xldump PWE   HON123D.xls PWE
$libinclude xldump PWM   HON123D.xls PWM
*$offtext